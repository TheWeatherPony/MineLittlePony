buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven'
		}
		maven {
			name = 'sponge'
			url = 'https://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:' + project.forgeGradleVersion
		classpath 'org.spongepowered:mixingradle:' + project.mixinGradleVersion
	}
}
apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'org.spongepowered.mixin'

version = project.mcVersion + '.' + project.buildVersion + '-' + project.buildType
group = project.group
description = project.name_mlp

minecraft {
	version = project.mcVersion
	mappings = project.mcMappings
	runDir = 'run'
	replace '@VERSION@', (project.mcVersion + '.' + project.buildVersion + '-' + project.buildType)
}

mixin {
	defaultObfuscationEnv notch
}

targetCompatibility = 1.8
sourceCompatibility = 1.8

sourceSets {
	hdskins {
		compileClasspath += main.compileClasspath
		ext.refMap = project.refCore_hd
	}
	main {
		compileClasspath += hdskins.output + hdskins.compileClasspath
		ext.refMap = project.refCore_mlp
	}
}

dependencies {
	// use the same version as httpclient
	hdskinsCompile('org.apache.httpcomponents:httpmime:4.3.2') {
		transitive = false
	}
}

processResources {
	def props = [
		version: project.mcVersion + '.' + project.buildVersion + '-' + project.buildType,
		mcversion: project.mcVersion,
		revision: project.buildRevision,
	]
	inputs.properties props
	from(sourceSets.main.resources.srcDirs) {
		include 'litemod.json'
		expand props
	}
	// copy everything else, thats not the litemod.json
	from(sourceSets.main.resources.srcDirs) {
		exclude 'litemod.json'
	}
}

jar {
	from sourceSets.hdskins.output

	// TODO relocate. LiteLoader excludes apache libs from classloading
	from {configurations.hdskinsCompile.collect{it.isDirectory() ? it : zipTree(it)}}
	
	classifier ""
}

reobf {
	srgJar {
		mappingType = 'SEARGE'
	}
}

sourceJar {
	// add hdskins sources
	from sourceSets.hdskins.allSource
}

task srgJar(type: Jar) {
	from sourceSets.main.output
	from sourceSets.hdskins.output
	
	classifier "srg"
	baseName "mod-${project.name.toLowerCase()}"
}

task skinZip(type: Zip, dependsOn: build) {
	from fileTree('skins')
	baseName project.skinsFile
	version (project.mcVersion + '.' + project.buildVersion + '-' + project.buildType)
	destinationDir = file('build/libs')
}

build.finalizedBy(skinZip)
